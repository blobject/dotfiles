#! /usr/bin/env tclsh

###############################################################################
# 0k: desktop utilities related to the keyboard or accessed by it

source $::env(HOME)/bin/lib.tcl

need notify-send \
     setxkbmap \
     xbacklight light \
     grim jq slurp swaymsg \
     ponymix

###############################################################################

proc lay {} {
  set arg [lindex $::argv 0]
  switch -- $arg {
    h - hsnt {
      set arg hsnt
      exec setxkbmap hsnt
    }
    q - qwerty - u - us {
      set arg qwerty
      exec setxkbmap -option ctrl:nocaps us
    }
    default {
      usage {lay h_snt|q_werty}
    }
  }
  exec notify-send -u low -t 2000 -i input-keyboard key $arg
}

###############################################################################

proc lum {} {
  global SESS

  set arg [lindex $::argv 0]
  set cmd {light}
  set cmd_set {-S}
  set cmd_dec {-U}
  set cmd_inc {-A}
  set cmd_get {-G}
  if {{x} eq $SESS} {
    set cmd {xbacklight}
    set cmd_set {-set}
    set cmd_dec {-dec}
    set cmd_inc {-inc}
    set cmd_get {-get}
  }
  set cmd_sub $cmd
  set notify {notify-send -u low -t 2000 -i redshift-status-}

  switch -- [string index $arg 0] {
    {} { append notify on }
    - { append cmd_sub " $cmd_dec"; append notify off; }
    + { append cmd_sub " $cmd_inc"; append notify on; }
    default { append cmd_sub " $cmd_set"; append notify on }
  }
  if {[string length $arg]} {
    set arg [string range $arg 1 end]
    exec {*}$cmd_sub $arg
  }
  exec {*}$notify lum [exec $cmd $cmd_get]%
}

###############################################################################

proc shot {} {
  global argc

  set one [lindex $::argv 0]
  set two [lindex $::argv 1]
  set three [lindex $::argv 2]
  set dst $::env(HOME)/got
  set name "shot-[exec date +%s]."
  set cmd {grim}
  set geom {}
  set notify {notify-send -u normal -i image-x-generic}

  proc help {} {
    upvar 1 dst dst
    return "\[active|*screen|select] \[jpg|*png] \[c]
  will save to: $dst"
  }

  if {3 < $argc - 1} {
    puts stderr "wrong number of arguments"
    usage "shot [help]"
  }

  if {{} ne $three} {
    if {{c} eq $three} {
      set three {-c }
    } else {
      puts stderr "unrecognised cursor enabling: $three"
      usage "shot [help]"
    }
  }
  if {{} eq $two} {
    set two png
  }
  if {{} eq $one} {
    set one screen
  }

  switch -- $two {
    j - jpg {
      append name jpg
      set two "$three-t jpeg -q 90"
    }
    p - png {
      append name png
      set two "$three-t png"
    }
    default {
      puts stderr "unrecognised image type: $two"
      usage "shot [help]"
    }
  }

  switch -- $one {
    active {
      set foc [exec swaymsg -t get_tree | jq -r {recurse(.nodes[]?, .floating_nodes[]?) | select(.focused)}]
      set geom [exec echo $foc | jq -r {.rect | "\(.x),\(.y) \(.width)x\(.height)"}]
      set geom [list -g $geom]
    }
    screen {}
    select {
      catch {set geom [exec swaymsg -t get_tree | jq -r {.. | select(.pid? and .visible?) | .rect | "\(.x),\(.y) \(.width)x\(.height)"} | slurp]}
      if {{} eq $geom} {
        puts "$ME: select shot canceled"
        exit
      }
      set geom [list -g $geom]
    }
    default {
      puts stderr "unrecognised command: $one"
      usage "shot [help]"
    }
  }
  exec {*}$cmd {*}$two {*}$geom $dst/$one$name
  exec {*}$notify shot $one$name
}

###############################################################################

proc vol {} {
  # vars
  set devs [dict create \
    laptop alsa_output.pci-0000_00_1f.3.analog-stereo \
    mic    alsa_input.pci-0000_00_1f.3.analog-stereo \
    cx     bluez_sink.00_1B_66_8C_6C_44.a2dp_sink \
    clip   bluez_sink.00_12_6F_EE_F0_F7.a2dp_sink ]
  set dev {}
  set ismic false
  set base "ponymix --sink --device "
  set cmd {}
  set notify {notify-send -u low -t 2000 -i}

  # which?
  if {{bt} ne [lindex $::argv 0]} {
    set dev laptop
  } else {
    set ::argv [lrange $::argv 1 end]
    if [catch {exec ponymix --sink list | grep {: bluez_}}] {
      exec {*}$notify network-bluetooth vol {no bt audio}
      exit
    } else {
      switch -- [exec ponymix --sink list \
                 | grep {: bluez_} | head -1 | cut {-d } -f3] \
        [dict get $devs cx]   { set dev cx } \
        [dict get $devs clip] { set dev clip } \
        default { fail {unrecognised device} }
    }
  }
  append base [dict get $devs $dev]
  set cmd $base

  # mic?
  if {{mic} eq [lindex $::argv 0]} {
    set ::argv [lrange $::argv 1 end]
    set ismic true
    set base "ponymix --source --device "
    if ![catch {exec ponymix --source list \
         | grep [exec echo [dict get $devs $dev] | cut -d. -f2] \
         | grep {\(source\.\|input\.\)} \
         | cut {-d } -f3} \
         micdev] {
      append base $micdev
    } else {
      set dev laptop
      append base [dict get $devs mic]
    }
    set cmd $base
  }

  # build command
  switch -- [string index [lindex $::argv 0] 0] {
    {} {}
    x { append cmd { toggle} }
    - { append cmd " decrease [string range $::argv 1 end]" }
    + { append cmd " increase [string range $::argv 1 end]" }
    default { usage {vol [bt] [mic] |x|-N|+N}}
  }

  # execute ponymix
  exec {*}$cmd

  # build notification
  switch -- [string index [lindex $::argv 0] 0] {
    {} {
      set arg " audio-ready"
      if {$ismic} { set arg " mic-ready" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
    x {
      set arg " audio-off vol \"$dev muted\""
      if {$ismic} { set arg " mic-off vol \"$dev muted\"" }
      if [catch {exec {*}$base is-muted}] {
        set arg " audio-on vol \"$dev on\""
        if {$ismic} { set arg " mic-on vol \"$dev on\"" }
      }
      append notify $arg
    }
    - {
      set arg " audio-volume-medium"
      if {$ismic} { set arg " microphone-sensitivity-medium" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
    + {
      set arg " audio-volume-high"
      if {$ismic} { set arg " microphone-sensitivity-high" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
  }
  exec {*}$notify
}

###############################################################################

set arg [lindex $::argv 0]
set ::argv [lrange $::argv 1 end]
switch -- $arg {
  lay  { lay; }
  lum  { lum; }
  shot { shot; }
  vol  { vol; }
  default {
    usage {lay|lum|shot|vol
  lay   keyboard layout    |h_hsnt|q_werty|b_ow
  lum   monitor luminance  |-N|+N
  shot  screenshot         |active|*screen*|select |jpg|*png* |c
  vol   audio volume       |bt |mic |x|-N|+N}
  }
}

