#! /usr/bin/env tclsh

###############################################################################
# 0t: utilities/shortcuts for the graphical shell

source $::env(HOME)/bin/lib.tcl

need notify-send rg \
     fnottctl \
     firefox \
     bluetoothctl \
     light \
     tofi-drun \
     setxkbmap \
     grim slurp swappy \
     ponymix

###############################################################################

proc __nrep {name} {
  # helper: replacement id for notify-send
  catch {exec fnottctl list | rg ": $name" -r {}} id ret
  if {0 != [dict get $ret -code]} {
    return {}
  }
  return "-r $id"
}

###############################################################################

proc _firefox {} {
  # for wm
  try {
    exec firefox --private-window --profile "$::env(HOME)/opt/firefox_proxy"
  } on error e { fail "$e" }
}

###############################################################################

proc _lum- {} {
  # for wm, replaces lum
  exec sleep 0.1
  exec notify-send {*}[__nrep lum] -u low -i redshift-status-off lum "[exec light -G]"
}

###############################################################################

proc _lum+ {} {
  # for wm, replaces lum
  exec sleep 0.1
  exec notify-send {*}[__nrep lum] -u low -i redshift-status-on lum "[exec light -G]"
}

###############################################################################

proc _run {} {
  # for wm
  try {
    exec tofi-drun \
--font $::env(XDG_DATA_HOME)/fonts/agave/Agave-Bold-slashed.ttf \
--hint-font=false \
{--height=100%} {--width=100%} \
{--padding-left=40%} {--padding-top=25%} \
--border-width=0 --outline-width=0 \
--num-results=16 \
--result-spacing=16 \
--background-color=000000a0 \
--input-color=d2d6deff \
--prompt-color=d2d6deff \
--selection-color=ffffffff \
--text-color=b9bdc5ff \
--drun-launch=true \
--fuzzy-match=true \
--late-keyboard-init \
{--prompt-text=> }
  } on error e { fail "$e" }
}

###############################################################################

proc btbat {{id {}}} {
  # for waybar
  if {{} eq $id} {
    global ME

    puts stderr "$ME: mac address required
  available devices:
  EarFun Free Pro 2    00:12:6F:EE:F0:F7
  Sennheiser CX 6.00BT 00:1B:66:8C:6C:44
  JBL Clip 2           B0:F1:A3:63:0A:66
  Corne keyboard       DD:A5:E3:61:B3:96"
    puts {}
    exit
  }
  catch {exec bluetoothctl info $id | rg {Connected: yes}} {} ret
  if {0 != [dict get $ret -code]} {
    puts {}
    exit
  }
  set icon { }
  if {{DD:A5:E3:61:B3:96} eq $id} { set icon { } }
  regsub -all {:} $id {_} id
  set blue "dbus-send \
--print-reply=literal \
--system \
--dest=org.bluez \
/org/bluez/hci0/dev_$id"
  catch {exec {*}$blue org.freedesktop.DBus.Introspectable.Introspect | rg {Battery1}} {} ret
  if {0 != [dict get $ret -code]} {
    puts {}
    exit
  }
  puts "$icon[lindex [exec {*}$blue org.freedesktop.DBus.Properties.Get string:org.bluez.Battery1 string:Percentage] end]"
}

###############################################################################

proc cpu {} {
  # for waybar
  set dir /sys/devices/system/cpu/cpu0
  set gov [readfile $dir/cpufreq/scaling_governor]
  set epb [readfile $dir/power/energy_perf_bias]
  switch -- [string index $gov 1] {
    e { set gov {^}}
    o { set gov {_}}
    default { set gov {?}}
  }
  puts "$gov$epb"
}

###############################################################################

proc lay {arg} {
  global SESS

  if {{x} ne $SESS} {
    exec notify-send -u low -i preferences-desktop-keyboard key {xorg not running}
    exit
  }

  switch -- $arg {
    h - hsnt {
      set arg hsnt
      exec setxkbmap hsnt
      exec xset r rate 250 30
    }
    q - qwerty - u - us {
      set arg qwerty
      exec setxkbmap -option ctrl:nocaps us
      exec xset r rate 250 30
    }
    default {
      ufail {lay h_snt|q_werty}
    }
  }
  exec notify-send -u low -i cinnamon-virtual-keyboard key $arg
}

###############################################################################

proc lum {arg} {
  # for wm
  set cmd {/usr/bin/sudo /usr/bin/light}
  set cmd_set {-S}
  set cmd_dec {-U}
  set cmd_inc {-A}
  set cmd_get {-G}
  set cmd_sub $cmd
  set notify {notify-send -u low -i redshift-status-}

  switch -- [string index $arg 0] {
    {} { append notify on }
    - { append cmd_sub " $cmd_dec"; append notify off }
    + { append cmd_sub " $cmd_inc"; append notify on }
    default { append cmd_sub " $cmd_set"; append notify on }
  }
  if {[string length $arg]} {
    set arg [string range $arg 1 end]
    exec {*}$cmd_sub $arg
  }
  exec {*}$notify {*}[__nrep lum] lum [exec {*}$cmd $cmd_get]%
}

###############################################################################

proc shot {args} {
  set one [lindex $args 0]
  set two [lindex $args 1]
  set three [lindex $args 2]
  set dir $::env(HOME)/got
  set opt {}
  set dst {}
  set desc {}
  set notify {notify-send -u normal -i image-x-generic}

  proc help {} {
    upvar 1 dir dir
    return "\[e_dited|*s_creen] \[j_pg|*p_ng] \[c]
  will save to: $dir"
  }

  if {3 < [llength $args]} {
    puts stderr "wrong number of arguments"
    ufail "shot [help]"
  }

  if {{} ne $three} {
    if {{c} eq $three} {
      append opt { -c}
    } else {
      puts stderr "unrecognised cursor enabling: $three"
      ufail "shot [help]"
    }
  }

  if {{} eq $two} {
    set two png
  }
  switch -- $two {
    j - jpg {
      set two jpg
      append opt { -t jpeg -q 90}
    }
    p - png {
      set two png
      append opt { -t png}
    }
    default {
      puts stderr "unrecognised image type: $two"
      ufail "shot [help]"
    }
  }

  if {{} eq $one} {
    set one screen
  }
  switch -- $one {
    e - edited {
      set one e
      append opt " -g"
      set desc "editing... editedshot-?.$two"
      set dst [list [exec slurp] - | swappy -f -]
    }
    s - screen {
      set desc screenshot-[exec date +%s].$two
      set dst $dir/$desc
    }
    default {
      puts stderr "unrecognised command: $one"
      ufail "shot [help]"
    }
  }

  if {{e} eq $one} {
    exec {*}$notify shot $desc
    exec grim {*}$opt {*}$dst
  } else {
    exec grim {*}$opt {*}$dst
    exec {*}$notify shot $desc
  }
}

###############################################################################

proc vol {args} {
  # for wm
  # vars
  set devs [dict create \
    laptop alsa_output.pci-0000_00_1f.3.analog-stereo \
    mic    alsa_input.pci-0000_00_1f.3.analog-stereo \
    cx     bluez_sink.00_1B_66_8C_6C_44.a2dp_sink \
    clip   bluez_sink.00_12_6F_EE_F0_F7.a2dp_sink ]
  set dev {}
  set ismic false
  set base "ponymix --sink --device "
  set cmd {}
  set notify "notify-send [__nrep vol] -u low -i"

  # which?
  if {{bt} ne [lindex $args 0]} {
    set dev laptop
  } else {
    set args [lrange $args 1 end]
    if [catch {exec ponymix --sink list | rg {: bluez_}}] {
      exec {*}$notify bluetooth-disabled vol {no bt audio}
      exit
    } else {
      switch -- [exec ponymix --sink list \
                 | rg {: bluez_} | head -1 | cut {-d } -f3] \
        [dict get $devs cx]   { set dev cx } \
        [dict get $devs clip] { set dev clip } \
        default { fail {unrecognised device} }
    }
  }
  append base [dict get $devs $dev]
  set cmd $base

  # mic?
  if {{mic} eq [lindex $args 0]} {
    set args [lrange $args 1 end]
    set ismic true
    set base "ponymix --source --device "
    if ![catch {exec ponymix --source list \
         | rg [exec echo [dict get $devs $dev] | cut -d. -f2] \
         | rg {\(source\.\|input\.\)} \
         | cut {-d } -f3} \
         micdev] {
      append base $micdev
    } else {
      set dev laptop
      append base [dict get $devs mic]
    }
    set cmd $base
  }

  # build command
  switch -- [string index [lindex $args 0] 0] {
    {} {}
    x { append cmd { toggle} }
    - { append cmd " decrease [string range $args 1 end]" }
    + { append cmd " increase [string range $args 1 end]" }
    default { ufail {vol [bt] [mic] |x|-N|+N} }
  }

  # execute ponymix
  exec {*}$cmd

  # build notification
  switch -- [string index [lindex $args 0] 0] {
    {} {
      set arg " audio-ready"
      if {$ismic} { set arg " mic-ready" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
    x {
      set arg " audio-off vol \"$dev muted\""
      if {$ismic} { set arg " mic-off vol \"$dev muted\"" }
      if [catch {exec {*}$base is-muted}] {
        set arg " audio-on vol \"$dev on\""
        if {$ismic} { set arg " mic-on vol \"$dev on\"" }
      }
      append notify $arg
    }
    - {
      set arg " audio-volume-medium"
      if {$ismic} { set arg " microphone-sensitivity-medium" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
    + {
      set arg " audio-volume-high"
      if {$ismic} { set arg " microphone-sensitivity-high" }
      append notify "$arg vol \"$dev [exec {*}$base get-volume]%\""
    }
  }
  exec {*}$notify
}

###############################################################################

proc weather {} {
  # for waybar
  catch {exec curl -s {https://wttr.in/?0AQT&format=%t%x}} weather ret
  if {0 != [dict get $ret -code]} {
    puts {}
    exit
  }
  if {{U} eq [string index $weather 0]} {
    puts {?}
    exit
  }
  regexp {^\+?([0-9]+.)C(.*)$} $weather weather deg sky
  switch -- $sky {
    o   { set sky  }
    =   { set sky  }
    mmm { set sky  }
    mm  { set sky  }
    m   { set sky  }
    /// { set sky  }
    //  { set sky  }
    /   { set sky  }
    .   { set sky  }
    x   { set sky  }
    x/  { set sky  }
    **  { set sky  }
    *   { set sky  }
    */* { set sky  }
    */  { set sky  }
    /!/ { set sky  }
    !/  { set sky  }
    *!* { set sky  }
  }
  puts "$deg$sky"
}

###############################################################################

set arg [lindex $::argv 0]
if {[expr {0 eq [llength [info procs $arg]]}]} {
  ufail {btbat|cpu|lay|lum|shot|vol|weather
  btbat   get battery level of bluetooth device
  cpu     get cpu governor
  lay     [gs]et keyboard layout   _|h_hsnt|q_werty
  lum     [gs]et screen brightness _|-N|+N
  shot    take screenshot          _|active|*screen*|select |jpg|*png* |c
  vol     [gs]et audio volume      _|bt |mic |x|-N|+N
  weather get weather report}
}
$arg {*}[lrange $::argv 1 end]

